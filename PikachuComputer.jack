class PikachuComputer {

    field int x, y, vy, jumpV, t, hitTime;
    field boolean faceright, ontheair, pastColliding, chasing;
    static int dx, sx, sy; // move per press, sizex, sizey
    constructor PikachuComputer new(boolean fr) {
        let x = 411;
        let y = 180;

        let faceright = fr;
        let ontheair = false;
        let pastColliding = false;
        let chasing = true;

        let sx = 30;
        let sy = 60;

        let dx = 12;
        let jumpV = -15;
        
        return this;
    }

    method void movex(boolean dir){
        if (dir){
            let x = x + dx;
        }
        else {
            let x = x - dx;
        }
        return;
    }

    method void movey() {
        var int dt, dy;
        let dt = Phys.getDt();

        let dy = dt*vy;
        let y = y + dy;
        return;
    }

    method void jump(){
        let vy = jumpV;
        return;
    }

    method void groundPrevent() {
        if (y > (Phys.getGround()-1-sy)) {
            let vy = 0;
            let y = Phys.getGround()-sy;
        }
        return;
    }

    method void gravity() {
        var int dvy;
        let dvy = Phys.getDvy();
        let vy = vy + dvy;
        return;
    }

    method void wallPrevent() {
        if(x > (512-1-sx)) {
            let x = (512-1-sx);
        }
        if(x < 246) {
            let x = 246;
        }
        return;
    }

    method void statusChange() {
        if (y > (Phys.getGround()-1-sy)) {
            let ontheair = false;
        }
        else {
            let ontheair = true;
        }
        return;
    }

    method void interactWithBall() {
        if (ontheair) {
            do smashBall(132);
        }
        else {
            do hitBall();
        }
        return;
    }

    method void smashBall(char smashDirection) {
        let chasing = true;
        if((smashDirection = 130) | (smashDirection = 132)) {
            do smashLeft();
        }
        return;
    }

    method void smashLeft() {
        var Ball b;
        let b = PhysicsManager.getBall();
        do b.setVx(-30);
        do b.setVy(-2);
        return;
    }

    method void hitBall() {
        var Ball b;
        let hitTime = t;
        let b = PhysicsManager.getBall();
        let chasing = false;
        do b.setVx(0);
        do b.setVy(-15);
        return;
    }

    method int collideWithBall() {
        var boolean isColliding;
        let isColliding = collisionDetection();
        if (isColliding) {
            if (pastColliding) {
                return false;
            }
            else {
                let pastColliding = true;
                return true;
            }
        }
        else {
            let pastColliding = false;
            return false;
        }
    }

    method boolean collisionDetection() {
        var Ball b;
        var int bx, by, r;
        let b = PhysicsManager.getBall();
        let bx = b.getx();
        let by = b.gety();
        let r = b.size();

        if (ballOnOtherSide()) {
            return false;
        }

        if (bx < x) {
            if (by < y) {
                return Util.le(Util.dist(bx, by, x, y), r);
            }
            if (by > (y+sy)) {
                return Util.le(Util.dist(bx, by, x, y+sy), r);
            }
            return (x-bx) < r;
        }

        if (bx > (x+sx)) {
            if (by < y) {
                return Util.le(Util.dist(bx, by, x+sx, y), r);
            }
            if (by > (y+sy)) {
                return Util.le(Util.dist(bx, by, x+sx, y+sy), r);
            }
            return (bx-x-sx) < r;
        }

        if (by < y) {
            return (y-by) < r;
        }
        if (by > (y+sy)) {
            return (by-y-sy) < r;
        }
        return true;
    }

    method void update() {
        let t = t + 1;
        if (collideWithBall()) {
            do interactWithBall();
        }
        do decide();
        do movey();
        do gravity();
        do groundPrevent();
        do wallPrevent();
        do statusChange();
        return;
    }


    method void render() {
        do Screen.drawRectangle(x, y, x+sx, y+sy);
        return;
    }

    method void decide() {
        if (chasing) {
            do moveToLanding();
            return;
        }
        if (~ontheair) {
            do waitOrJump();
        }
        return;
    }

    method void waitOrJump() {
        if ((t - hitTime) > 6) {
            do jump();
        }
        return;
    }

    method void moveToLanding() {
        var int nx, mx;
        var Rod r;

        let r = PhysicsManager.getRod();
        
        let nx = Util.calculateLanding(PhysicsManager.getBall(), y, y+sy);
        if (nx < r.getLeft()) {
            return;
        }
        // do Screen.drawRectangle(nx, 0, nx+15, 20);
        let mx = x+(sx/2);
        if (nx < mx) {
            do movex(0);
        }
        else {
            do movex(-1);
        }
        return;
    }

    method boolean ballOnOtherSide() {
        var Rod r;
        var Ball b;
        let r = PhysicsManager.getRod();
        let b = PhysicsManager.getBall();
        return (b.getx() < r.getLeft());
    }

    method void reset(){
        let x = 411;
        let y = 180;
        let vy = 0;
        
        let faceright = false;
        let ontheair = false;
        let chasing = true;
        let pastColliding = false;

        return;
    }
}