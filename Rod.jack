class Rod {
    field int x, y, sx, sy;

    constructor Rod new() {
        let x = 244;
        let y = 155;
        let sx = 2;
        let sy = 100;

        return this;
    }

    method void interactWithBall() {
        var Ball b;
        var int bx, by, r, px, py;
        
        let b = PhysicsManager.getBall();
        let bx = b.getx();
        let by = b.gety();
        let px = b.getPx();
        let py = b.getPy();
        let r = b.size();

        if (((py+r) < y) & ((by+r) < y)) {
            return; // fly on top
        }

        if (((px+r) < x) & ((bx+r) < x)) {
            return; // stay on left
        }

        if (((px-r) > (x+sx)) & ((bx-r) > (x+sx))) {
            return; // stay on right
        }

        if (b.getvx() = 0) {
            return;
        }

        if(((px+r) < x) & (b.getvx() > 0)) {
            do b.setVx(-Math.abs(b.getvx()) * 7 / 10);
            do b.setx(x-r);
            return;
        }
        if (((px-r) > (x+sx)) & (b.getvx() < 0)){
            do b.setVx(Math.abs(b.getvx()) * 7 / 10);
            do b.setx(x+sx+r);
        }
        return;
    }

    method void update(){
        do interactWithBall();
        return;
    }

    method int getRight() {
        return x + sx;
    }

    method int getLeft() {
        return x;
    }
}