class Rod {
    field int x, y, sx, sy;

    constructor Rod new() {
        let x = 244;
        let y = 155;
        let sx = 2;
        let sy = 100;

        return this;
    }

    method void interactWithBall() {
        var Ball b;
        var int bx, by, r, px, py;
        
        let b = PhysicsManager.getBall();
        let bx = b.getx();
        let by = b.gety();
        let px = b.getPx();
        let py = b.getPy();
        let r = b.size();

        if (((py+r) < y) & ((by+r) < y)) {
            return; // fly on top
        }

        if (((px+r) < x) & ((bx+r) < x)) {
            return; // stay on left
        }

        if (((px-r) > (x+sx)) & ((bx-r) > (x+sx))) {
            return; // stay on right
        }

        if((px+r) < x) {
            do b.setVx(-Math.abs(b.getvx()) * 7 / 10);
            do b.setx(x-r);
        }
        else {
            do b.setVx(Math.abs(b.getvx()) * 7 / 10);
            do b.setx(x+sx+r);
        }
        return;
    }

    method boolean collideWithBall() {
        var Ball b;
        var int bx, by, r;
        let b = PhysicsManager.getBall();
        let bx = b.getx();
        let by = b.gety();
        let r = b.size();

        if (bx < x) {
            if (by > y) {
                return Util.le(Util.dist(bx, by, x, by), r);
            }
        }
        if (bx > (x+sx)) {
            if (by > y) {
                return Util.le(Util.dist(bx, by, x+sx, by), r);
            }
        }
        if ((bx < x) & (bx > (x+sx))){
            return Util.le(Util.dist(bx, by, bx, y), r);
        }
        
        return false;
    }

    method void update(){
        if (collideWithBall()) {
            do interactWithBall();
        }
        return;
    }

    method void render() {
        do Screen.drawRectangle(x, y, x+sx, y+sy);
        return;
    }

    method int getRight() {
        return x + sx;
    }

    method int getLeft() {
        return x;
    }
}