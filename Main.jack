class Main {

    static int t, updaterate;
    static boolean selecting, playing;

    static Pikachu p;
    static PikachuComputer pc;
    static Ball b;
    static Rod r;
    static Point a;
    static Floor f;

    function void main() {
        
        let p = Pikachu.new(true);
        let pc = PikachuComputer.new(false);
        let b = Ball.new(100, 30);
        let r = Rod.new();
        let a = Point.new();
        let f = Floor.new();

        let selecting = true;
        let playing = false;
        let t = 0;
        let updaterate = 20;

        do Phys.init();
        do PhysicsManager.init(p, pc, b, r, f);
        do ScreenManager.init(p, pc, b, r, f);
        do Draw.init();

        do ScreenManager.mainlobby();

        while(selecting){
            do a.update();
            if (a.getstart()){
                let selecting = false;
                let playing = true;
            }
        }

        do Draw.drawpreparing();

        while(playing) {
            // TODO: split rendering and updating
            do PhysicsManager.update();
            do ScreenManager.render();
            let t = t + 1;
            if (t = 6000) {
                let t = 0;
            }
            do Sys.wait(updaterate);
        }
        return;
    }

    function void setupdaterate(int x){
        if (x = 1){
            let updaterate = 50;
        }
        if (x = 2){
            let updaterate = 30;
        }
        if (x = 3){
            let updaterate = 20;
        }
        return;
    }

    function void reset(){
        do p.reset();
        do pc.reset();
        do b.reset(f.getxwin());
        return;
    }
}