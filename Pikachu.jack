class Pikachu{

    field int x, y, vy, t, stopTime, smashTime, step;
    field char smashDirection;
    field boolean faceright, ontheair, sliding, stopping, smashing, pastColliding;
    static int dx, slide_x, sx, sy; // move per press, sizex, sizey
    constructor Pikachu new(boolean fr) {
        let x = 100;
        let y = 180;
        let step = 0;

        let faceright = fr;
        let ontheair = false;
        let sliding = false;
        let stopping = false;
        let smashing = false;
        let pastColliding = false;

        let sx = 40;
        let sy = 60;

        let dx = 5;
        let slide_x = 10;
        return this;
    }

    method void movex(boolean dir){
        if (dir){
            let x = x + dx;
        }
        else {
            let x = x - dx;
        }
        return;
    }

    method void slidex(boolean dir){
        if (dir){
            let x = x + slide_x;
        }
        else{
            let x = x - slide_x;
        }
        return;
    }

    method void movey() {
        var int dt, dy;
        let dt = Phys.getDt();

        let dy = dt*vy;
        let y = y + dy;
        return;
    }

    method void jump(){
        let vy = -15;
        return;
    }

    method void groundPrevent() {
        if (y > (Phys.getGround()-1-sy)) {
            let vy = 0;
            let y = Phys.getGround()-sy;
        }
        return;
    }

    method void gravity() {
        var int dvy;
        let dvy = Phys.getDvy();
        let vy = vy + dvy;
        return;
    }

    method void wallPrevent() {
        if(x < 0) {
            let x = 0;
        }
        if(x > (244-1-sx)) {
            let x = 244-sx;
        }
        return;
    }

    method void swapAxis() {
        var int temp;
        let temp = sx;
        let sx = sy;
        let sy = temp;
        do groundPrevent();
        return;
    }

    method void statusChange() {
        if (y > (Phys.getGround()-1-sy)) {
            let ontheair = false;
            let smashing = false;
            if (sliding){
                let stopping = true;
                let sliding = false;
                do swapAxis();
            }
        }
        else {
            let ontheair = true;
        }
        return;
    }

    method void interactWithBall() {
        var Ball b;
        let b = PhysicsManager.getBall();
        if (smashing) {
            do smashBall();
        }
        else {
            do hitBall();
        }
        return;
    }

    method void smashBall() {
        var int bx, by;
        var boolean decided;
        var Ball b;
        let bx = 6;
        let by = 0;
        let b = PhysicsManager.getBall();
        let smashing = false;
        if ((smashDirection = 130) | (smashDirection = 132)) {
            let bx = 20;
            let by = -2;
            let decided = true;
        }
        if (smashDirection = 131) {
            let bx = 20;
            let by = -15;
            let decided = true;
        }
        if (smashDirection = 133) {
            let bx = 15;
            let by = 10;
            let decided = true;
        }
        if(~decided) {
            let bx = 10;
            let by = 0;
        }
        do b.setVx(bx);
        do b.setVy(by);
        return;
    }

    method void hitBall() {
        var Ball b;
        var int nvx;
        let nvx = 15;
        let b = PhysicsManager.getBall();
        do b.setVy(-nvx);
        if (sliding) {
            if (~faceright) {
                let nvx = -nvx;
            }
            do b.setVx(nvx);
            return;
        }
        if (ontheair | (b.getvx() = 0)) {
            do b.setVx(nvx);
        }
        else {
            do b.setVx(0);
        }
        return;
    }

    method int collideWithBall() {
        var boolean isColliding;
        let isColliding = collisionDetection();
        if (isColliding) {
            if (pastColliding) {
                return false;
            }
            else {
                let pastColliding = true;
                return true;
            }
        }
        else {
            let pastColliding = false;
            return false;
        }
    }

    method boolean collisionDetection() {
        var Ball b;
        var int bx, by, r;
        let b = PhysicsManager.getBall();
        let bx = b.getx();
        let by = b.gety();
        let r = b.size();

        if (ballOnOtherSide()) {
            return false;
        }

        if (bx < x) {
            if (by < y) {
                return Util.le(Util.dist(bx, by, x, y), r);
            }
            if (by > (y+sy)) {
                return Util.le(Util.dist(bx, by, x, y+sy), r);
            }
            return (x-bx) < r;
        }

        if (bx > (x+sx)) {
            if (by < y) {
                return Util.le(Util.dist(bx, by, x+sx, y), r);
            }
            if (by > (y+sy)) {
                return Util.le(Util.dist(bx, by, x+sx, y+sy), r);
            }
            return (bx-x-sx) < r;
        }

        if (by < y) {
            return (y-by) < r;
        }
        if (by > (y+sy)) {
            return (by-y-sy) < r;
        }
        return true;
    }

    method void update() {
        let t = t + 1;
        if (collideWithBall()) {
            do interactWithBall();
        }
        if (stopping) {
            if ((t - stopTime) > 15) {
                let stopping = false;
            }
            return;
        }
        if (sliding){
            do slidex(faceright);
        }
        else{
            do keyInput();            
        }
        do movey();
        do gravity();
        do statusChange();
        do groundPrevent();
        do wallPrevent();
        return;
    }


    method void render() {
        if (sliding) {
            if (faceright) {
                do Draw.drawPikachuSlideRight(x, y, 0);
            }
            else {
                do Draw.drawPikachuSlideLeft(x, y, 0);
            }
        }
        else {
            do Draw.drawPikachufaceright(x, y, step);
            let step = step + 1;
            if (step = 10){
                let step = 0;
            }
        }
        return;
    }

    method void spike() {
        let smashing = true;
        let smashDirection = 0;
        return;
    }

    method void slide(){
        let vy = -5;
        let ontheair = true;
        let sliding = true;
        let stopTime = t;
        do swapAxis();
        return;
    }

    method boolean isValidSmashDirection(char c) {
        return (129 < c) & (c < 134); // is an arrow key
    }

    method void keyInput() {
        var char c;
        let c = Keyboard.keyPressed();
        if(smashing) {
            if (isValidSmashDirection(c)) {
                let smashDirection = c;
            }
        }
        if (c = 130){
            do movex(0);
            let faceright = false ;    //left
        }
        if (c = 132) {
            do movex(-1);
            let faceright = true ;     //right
        }
        if (~(ontheair) & c = 131){    // up
            do jump();
        }
        if (c = 128) { // enter
            if (ontheair) {
                do spike();
            }
            else {
                do slide();
            }
        }
        return;
    }

    method boolean ballOnOtherSide() {
        var Rod r;
        var Ball b;
        let r = PhysicsManager.getRod();
        let b = PhysicsManager.getBall();
        return (b.getx() > r.getRight());
    }

    method int getx() {
        return x + (sx/2);
    }

    method int gety() {
        return y + (sy/2);
    }

    method boolean getOnTheAir() {
        return ontheair;
    }

    method void reset(){
        let x = 100;
        let y = 180;
        let vy = 0;
        
        let faceright = true;
        let ontheair = false;
        if (sliding) {
            do swapAxis();
        }
        let sliding = false;
        let stopping = false;

        return;
    }
}