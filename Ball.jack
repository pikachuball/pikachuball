class Ball {

    field int x, y, vx, vy, px, py, rotate;

    static int r;

    constructor Ball new(int nx, int ny) {
        let x = nx;
        let y = ny;
        let vx = 0;
        let vy = 0;
        let rotate = 0;

        let r = 30;
        return this;
    }

    method void move() {
        var int dt, dx, dy;
        let dt = Phys.getDt();
        let dx = vx * dt;
        let dy = vy * dt;
        let x = x + dx;
        let y = y + dy;

        return;
    }

    method int getx() {
        return x;
    }
    
    method int gety() {
        return y;
    }

    method int getvx(){
        return vx;
    }

    method int getvy(){
        return vy;
    }

    method int size() {
        return r;
    }

    method void gravity() {
        var int dvy;
        let dvy = Phys.getDvy();
        let vy = vy + dvy;
        return;
    }
    
    method void boundsPrevent() {
        var Floor f;
        let f = PhysicsManager.getFloor();
        do f.setDeterHitTheFloor(false);
        if (y > (Phys.getGround()-r-1)) {
            let vy = -Math.abs(vy) * 7 / 10;
            let y = Phys.getGround()-r;

            if (vy > -3) {
                let vy = 0;
            }
            
            do f.setDeterHitTheFloor(true);
            do f.setpoint(x);
        }
        if (y < (r+1)) {
            let vy = Math.abs(vy);
            let y = r;
        }
        if (x > (511-r-1)) {
            let vx = -Math.abs(vx);
            let x = 511-r;
        }
        if (x < (r+1)) {
            let vx = Math.abs(vx);
            let x = r;
        }
        return;
    }

    // updating in the physical world
    method void update() {
        let px = x;
        let py = y;
        // stores the previous coordinate
        do move();
        do gravity();
        do boundsPrevent();
        return;
    }

    method void render() {
        do Draw.drawBall(x, y, rotate);
        let rotate = rotate + 1;
        if (rotate = 10){
            let rotate = 0;
        }
        return;
    }

    method void setVx(int v) {
        let vx = v;
        return;
    }

    method void setVy(int v) {
        let vy = v;
        return;
    }

    method int getPx() {
        return px;
    }
    
    method int getPy() {
        return py;
    }

    method void setx(int nx) {
        let px = x;
        
        let x = nx;
        return;
    }

    method void reset(boolean xwin){
        if (xwin){
            let x = 100;
            let y = 30;
            let vx = 0;
            let vy = 0;
        }
        else{
            let x = 411;
            let y = 30;
            let vx = 0;
            let vy = 0;
        }
        return;
    }
}